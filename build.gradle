buildscript {
  ext {
    confluentVersion = '5.5.0'
    kafkaVersion = '2.5.0'
    lombokVersion = '1.18.12'
  }

  repositories {
    jcenter()
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    // check the latest version
    classpath "com.commercehub.gradle.plugin:gradle-avro-plugin:0.14.2"
  }
}

plugins {
  id 'java'
  id 'application'
  id 'groovy'
  id 'idea'
  id "io.freefair.lombok" version "5.0.1"
}

apply plugin: "com.commercehub.gradle.plugin.avro"

sourceCompatibility = 1.8

repositories {
  jcenter()
  maven { url 'https://packages.confluent.io/maven/' }
  maven { url 'https://jitpack.io' }
}

dependencies {
  // check the latest version
  implementation 'org.apache.avro:avro:1.8.2'
  implementation "org.apache.kafka:kafka-clients:${kafkaVersion}"
  implementation "io.confluent:kafka-streams-avro-serde:${confluentVersion}"

  // config loader, topology visualization util
  implementation("com.github.gAmUssA:streaming-utils:-SNAPSHOT") {
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
  }

  implementation "org.slf4j:slf4j-log4j12:1.7.30"
  implementation "org.apache.kafka:kafka-streams:${kafkaVersion}"
  implementation "io.confluent:monitoring-interceptors:${confluentVersion}"

  // Use the latest Groovy version for Spock testing
  implementation 'org.codehaus.groovy:groovy-all:2.5.6'

  // Use the awesome Spock testing and specification framework even with Java
  testImplementation 'org.spockframework:spock-core:1.2-groovy-2.5'
  testImplementation 'junit:junit:4.12'

  // Kafka Streams Topology TestRunner
  testImplementation "org.apache.kafka:kafka-streams-test-utils:${kafkaVersion}"
  testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
  testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

avro {
  stringType = "String"
}
// Define the main class for the application
mainClassName = 'io.confluent.demo.StreamsDemo'
